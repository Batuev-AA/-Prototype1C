
&НаСервере
Процедура ЗаполнитьПоДокументу(Документ, ТипДокумента)
	
	Если ЗначениеЗаполнено(Документ) Тогда
		Если Год(Документ.Дата)>3999 Тогда 
			ДатаДокумента = ДобавитьМесяц(Документ.Дата,-24000); //Уберем смещение 2000
		Иначе
			ДатаДокумента = Документ.Дата;
		КонецЕсли;	
		
		Запись.Период = НачалоДня(ДатаДокумента);
		Запись.ДокументХозяйственнойОперации = Документ;
		Запись.ТипДокумента = ТипДокумента;
		
		Если ТипДокумента = "ПриходныйКассовыйОрдер" ИЛИ ТипДокумента = "ПоступлениеНаРасчетныйСчет" Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	Храм_КнигаУчета.ДокументХозяйственнойОперации КАК ДокументХозяйственнойОперации,
			|	СУММА(Храм_КнигаУчета.СуммаПриход) КАК СуммаПриход
			|ИЗ
			|	РегистрСведений.Храм_КнигаУчета КАК Храм_КнигаУчета
			|ГДЕ
			|	Храм_КнигаУчета.ДокументХозяйственнойОперации = &ДокументХозяйственнойОперации
			|
			|СГРУППИРОВАТЬ ПО
			|	Храм_КнигаУчета.ДокументХозяйственнойОперации";
			
			Запрос.УстановитьПараметр("ДокументХозяйственнойОперации", Запись.ДокументХозяйственнойОперации);
			
			Результат = Запрос.Выполнить();
			Выборка = Результат.Выбрать();
			
			УжеВведенныеСуммы = 0;
			пока Выборка.Следующий() Цикл
				УжеВведенныеСуммы = Выборка.СуммаПриход;
			КонецЦикла;
			
			Запись.СуммаПриход = Документ.СуммаДокумента - УжеВведенныеСуммы;
			
		ИначеЕсли ТипДокумента = "РасходныйКассовыйОрдер" ИЛИ ТипДокумента = "СписаниеСРасчетногоСчета" Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	Храм_КнигаУчета.ДокументХозяйственнойОперации КАК ДокументХозяйственнойОперации,
			|	СУММА(Храм_КнигаУчета.СуммаРасход) КАК СуммаРасход
			|ИЗ
			|	РегистрСведений.Храм_КнигаУчета КАК Храм_КнигаУчета
			|ГДЕ
			|	Храм_КнигаУчета.ДокументХозяйственнойОперации = &ДокументХозяйственнойОперации
			|
			|СГРУППИРОВАТЬ ПО
			|	Храм_КнигаУчета.ДокументХозяйственнойОперации";
			
			Запрос.УстановитьПараметр("ДокументХозяйственнойОперации", Запись.ДокументХозяйственнойОперации);
			
			Результат = Запрос.Выполнить();
			Выборка = Результат.Выбрать();
			
			УжеВведенныеСуммы = 0;
			пока Выборка.Следующий() Цикл
				УжеВведенныеСуммы = Выборка.СуммаРасход;
			КонецЦикла;
			
			Запись.СуммаРасход = Документ.СуммаДокумента - УжеВведенныеСуммы;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	ЭтоПриход = (Запись.ТипДокумента = "ПриходныйКассовыйОрдер" ИЛИ Запись.ТипДокумента = "ПоступлениеНаРасчетныйСчет");
	ЭтоРасход = (Запись.ТипДокумента = "РасходныйКассовыйОрдер" ИЛИ Запись.ТипДокумента = "СписаниеСРасчетногоСчета");
	
	Элементы.Ящик.Видимость               = ЭтоПриход;
	Элементы.НазначениеПоступления.Видимость = ЭтоПриход;
	Элементы.СуммаПоступления.Видимость   = ЭтоПриход;
	Элементы.НаправлениеРасхода.Видимость = ЭтоРасход;
	Элементы.СуммаРасхода.Видимость       = ЭтоРасход;
	
КонецПроцедуры	

&НаКлиенте
Процедура ДокументХозяйственнойОперацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("ПриходныйКассовыйОрдер",    "Поступление наличных");
	СписокВыбора.Добавить("РасходныйКассовыйОрдер",    "Выдача наличных");
	СписокВыбора.Добавить("ПоступлениеНаРасчетныйСчет","Поступление на расчетный счет");
	СписокВыбора.Добавить("СписаниеСРасчетногоСчета",  "Списание с расчетного счета");
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораВидаДокумента", ЭтаФорма);
	ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораВидаДокумента(ЭлементСписка, ДопПараметры) Экспорт
	Если ЭлементСписка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы  = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина);
    ОбработкаВыбора = Новый ОписаниеОповещения("ВыбратьДокументЗавершение", ЭтаФорма, ЭлементСписка.Значение);
     
    ОткрытьФорму("ВнешнийИсточникДанных.БухгалтерияПредприятия30.Таблица."+ЭлементСписка.Значение+".ФормаВыбора", ПараметрыФормы,
        ЭтаФорма, , , , ОбработкаВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДокументЗавершение(Документ, ДопПараметры) Экспорт
    Если Документ = Неопределено Тогда
        Возврат;
    КонецЕсли;
	
	ЗаполнитьПоДокументу(Документ, ДопПараметры);
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Запись.СуммаПриход) ИЛИ ЗначениеЗаполнено(Запись.СуммаРасход) Тогда
		Элементы.ДокументХозяйственнойОперации.ТолькоПросмотр = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если (Запись.ТипДокумента = "ПриходныйКассовыйОрдер" ИЛИ Запись.ТипДокумента = "ПоступлениеНаРасчетныйСчет") И 
		НЕ ЗначениеЗаполнено(Запись.СуммаПриход) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не заполнена сумма прихода.'");
		Сообщение.Поле = "Запись.СуммаПриход";
		Сообщение.УстановитьДанные(Запись);
		Сообщение.Сообщить();
		
		Отказ = Истина;
		
	ИначеЕсли (Запись.ТипДокумента = "РасходныйКассовыйОрдер" ИЛИ Запись.ТипДокумента = "СписаниеСРасчетногоСчета") И 
		НЕ ЗначениеЗаполнено(Запись.СуммаРасход) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не заполнена сумма расхода.'");
		Сообщение.Поле = "Запись.СуммаРасход";
		Сообщение.УстановитьДанные(Запись);
		Сообщение.Сообщить();
		
		Отказ = Истина;
		
	КонецЕсли;	  

КонецПроцедуры
