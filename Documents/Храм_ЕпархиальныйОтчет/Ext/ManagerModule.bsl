#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 20, 0);
	
КонецФункции

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Бухгалтерская справка
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Храм_ЕпархиальныйОтчет";
	КомандаПечати.Представление = НСтр("ru = 'Епархиальный отчет'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
КонецПроцедуры

Функция ПечатьЕпархиальныйОтчет(МассивОбъектов, ОбъектыПечати) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	// Зададим параметры макета по умолчанию
	ТабДокумент.РазмерКолонтитулаСверху = 0;
	ТабДокумент.РазмерКолонтитулаСнизу  = 0;
	ТабДокумент.АвтоМасштаб             = Истина;
	ТабДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Портрет;
	ТабДокумент.ИмяПараметровПечати     = "ПАРАМЕТРЫ_ПЕЧАТИ_Храм_ЕпархиальныйОтчет_Храм_ЕпархиальныйОтчет";
	
	ОбщегоНазначения.УстановитьМинимальныеПоляПечати(ТабДокумент); //Измененно Батуев А.А.
	
	Макет = ОбщегоНазначения.МакетПечатнойФормы("Документ.Храм_ЕпархиальныйОтчет.ПФ_MXL_Храм_ЕпархиальныйОтчет"); //Измененно Батуев А.А.
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйДвиженияССубконто.НомерСтроки КАК НомерСтроки,
	|	ХозрасчетныйДвиженияССубконто.СчетДт,
	|	ХозрасчетныйДвиженияССубконто.ПодразделениеДт,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	|	ХозрасчетныйДвиженияССубконто.СчетКт,
	|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	|	ХозрасчетныйДвиженияССубконто.Организация,
	|	ХозрасчетныйДвиженияССубконто.ВалютаДт,
	|	ХозрасчетныйДвиженияССубконто.ВалютаКт,
	|	ХозрасчетныйДвиженияССубконто.Сумма,
	|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаДт,
	|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаКт,
	|	ХозрасчетныйДвиженияССубконто.КоличествоДт,
	|	ХозрасчетныйДвиженияССубконто.КоличествоКт,
	|	ХозрасчетныйДвиженияССубконто.Содержание,
	|	ХозрасчетныйДвиженияССубконто.Регистратор
	|ПОМЕСТИТЬ ВТХозрасчетный
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(, , Регистратор В (&МассивОбъектов), , ) КАК ХозрасчетныйДвиженияССубконто
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ХозрасчетныйДвиженияССубконто.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияБух.Ссылка КАК Ссылка,
	|	ОперацияБух.Номер,
	|	ОперацияБух.Ответственный КАК Ответственный,
	|	ОперацияБух.Дата,
	|	ОперацияБух.Содержание КАК СодержаниеОперации,
	|	ВТХозрасчетный.НомерСтроки КАК НомерСтроки,
	|	ВТХозрасчетный.СчетДт,
	|	ВТХозрасчетный.ПодразделениеДт,
	|	ВТХозрасчетный.СубконтоДт1,
	|	ВТХозрасчетный.СубконтоДт2,
	|	ВТХозрасчетный.СубконтоДт3,
	|	ВТХозрасчетный.СчетКт,
	|	ВТХозрасчетный.ПодразделениеКт,
	|	ВТХозрасчетный.СубконтоКт1,
	|	ВТХозрасчетный.СубконтоКт2,
	|	ВТХозрасчетный.СубконтоКт3,
	|	ВТХозрасчетный.Организация КАК Организация,
	|	ВТХозрасчетный.ВалютаДт,
	|	ВТХозрасчетный.ВалютаКт,
	|	ВТХозрасчетный.Сумма,
	|	ВТХозрасчетный.ВалютнаяСуммаДт,
	|	ВТХозрасчетный.ВалютнаяСуммаКт,
	|	ВТХозрасчетный.КоличествоДт,
	|	ВТХозрасчетный.КоличествоКт,
	|	ВТХозрасчетный.Содержание КАК Содержание
	|ИЗ
	|	Документ.ОперацияБух КАК ОперацияБух
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТХозрасчетный КАК ВТХозрасчетный
	|		ПО ОперацияБух.Ссылка = ВТХозрасчетный.Регистратор
	|ГДЕ
	|	ОперацияБух.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОперацияБух.Дата,
	|	ОперацияБух.Ссылка,
	|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		// Получаем области макета для вывода в табличный документ.
		ШапкаДокумента   = Макет.ПолучитьОбласть("Шапка");
		ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		СтрокаТаблицы    = Макет.ПолучитьОбласть("СтрокаТаблицы");
		ПодвалТаблицы    = Макет.ПолучитьОбласть("ПодвалТаблицы");
		ПодвалДокумента  = Макет.ПолучитьОбласть("Подвал");
				
		// Выведем шапку документа.
		СведенияОбОрганизации = ОбщегоНазначения.СведенияОЮрФизЛице(Выборка.Организация, Выборка.Дата); //Измененно Батуев А.А.
		
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("Организация",    ОбщегоНазначения.ОписаниеОрганизации(СведенияОбОрганизации)); //Измененно Батуев А.А.
		СтруктураШапки.Вставить("НомерДокумента", ОбщегоНазначения.НомерНаПечать(Выборка.Номер, Истина, Ложь)); //Измененно Батуев А.А.
		СтруктураШапки.Вставить("ДатаДокумента",  Формат(Выборка.Дата, "ДЛФ=D"));
		СтруктураШапки.Вставить("Содержание",     Выборка.СодержаниеОперации);
		
		ШапкаДокумента.Параметры.Заполнить(СтруктураШапки);
		ТабДокумент.Вывести(ШапкаДокумента);
		
		// Выведем заголовок таблицы.
		ТабДокумент.Вывести(ЗаголовокТаблицы);
		
		// Выведем строки документа.
		Пока Выборка.Следующий() Цикл
			
			СтрокаТаблицы.Параметры.Заполнить(Выборка);
			
			АналитикаДт = ?(ЗначениеЗаполнено(Выборка.ПодразделениеДт), Строка(Выборка.ПодразделениеДт) + Символы.ПС, "")
				+ ?(ЗначениеЗаполнено(Выборка.СубконтоДт1), Строка(Выборка.СубконтоДт1) + Символы.ПС, "")
				+ ?(ЗначениеЗаполнено(Выборка.СубконтоДт2), Строка(Выборка.СубконтоДт2) + Символы.ПС, "")
				+ ?(ЗначениеЗаполнено(Выборка.СубконтоДт3), Строка(Выборка.СубконтоДт3), "");
			
			АналитикаКт = ?(ЗначениеЗаполнено(Выборка.ПодразделениеКт), Строка(Выборка.ПодразделениеКт) + Символы.ПС, "")
				+ ?(ЗначениеЗаполнено(Выборка.СубконтоКт1), Строка(Выборка.СубконтоКт1) + Символы.ПС, "")
				+ ?(ЗначениеЗаполнено(Выборка.СубконтоКт2), Строка(Выборка.СубконтоКт2) + Символы.ПС, "")
				+ ?(ЗначениеЗаполнено(Выборка.СубконтоКт3), Строка(Выборка.СубконтоКт3), "");
				
			СтруктураАналитики = Новый Структура("АналитикаДт,АналитикаКт", АналитикаДт, АналитикаКт);
			СтрокаТаблицы.Параметры.Заполнить(СтруктураАналитики);
			
			// Проверим, помещается ли строка с подвалом.
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(СтрокаТаблицы);
			СтрокаСПодвалом.Добавить(ПодвалТаблицы);
			СтрокаСПодвалом.Добавить(ПодвалДокумента);
			
			Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаСПодвалом) Тогда //Измененно Батуев А.А.
				
				// Выведем подвал таблицы.
				ТабДокумент.Вывести(ПодвалТаблицы);
					
				// Выведем разрыв страницы.
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

				// Выведем заголовок таблицы.
				ТабДокумент.Вывести(ЗаголовокТаблицы);
				
			КонецЕсли;
			
			ТабДокумент.Вывести(СтрокаТаблицы);
			
		КонецЦикла;
		
		// Выведем подвал таблицы.
		ТабДокумент.Вывести(ПодвалТаблицы);
		
		// Выведем подвал документа.
		ТабДокумент.Вывести(ПодвалДокумента);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		ОбщегоНазначения.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);  //Измененно Батуев А.А.

	КонецЦикла;

	Возврат ТабДокумент;

КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм,
	ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Проверяем, нужно ли для макета ПлатежноеПоручение формировать табличный документ.
	Если ОбщегоНазначения.НужноПечататьМакет(КоллекцияПечатныхФорм, "Храм_ЕпархиальныйОтчет") Тогда //Измененно Батуев А.А.
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		ОбщегоНазначения.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Храм_ЕпархиальныйОтчет", "Епархиальный отчет", 
			ПечатьЕпархиальныйОтчет(МассивОбъектов, ОбъектыПечати), , "Документ.ПФ_MXL_Храм_ЕпархиальныйОтчет.ПФ_MXL_Храм_ЕпархиальныйОтчет"); //Измененно Батуев А.А.
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
